SELECT
	S.[name] AS SchemaName
	,T.[name] AS TableName
	,I.[name] AS IndexName
	,I.index_id
	,I.[type_desc]
	,SUM(PS.used_page_count) * 8 / 1024.0 AS IndexSizeMB
	,COUNT(*) AS [Partitions]
	,MAX(PS.row_count) AS [Rows]
FROM
	sys.schemas S
	JOIN sys.tables T ON S.[schema_id] = T.[schema_id]
	JOIN sys.indexes I ON T.[object_id] = I.[object_id]
	JOIN sys.dm_db_partition_stats PS ON I.[object_id] = PS.[object_id] AND I.index_id = PS.index_id
GROUP BY
	S.[name]
	,T.[name]
	,I.[name]
	,I.index_id
	,I.[type_desc];

SELECT
	S.[name] AS SchemaName
	,T.[name] AS TableName
	,I.[name] AS IndexName
	,I.index_id
	,I.[type_desc]
	,PS.[partition_id]
	,PS.used_page_count * 8 / 1024.0 AS IndexSizeMB
	,PS.row_count AS [Rows]
FROM
	sys.schemas S
	JOIN sys.tables T ON S.[schema_id] = T.[schema_id]
	JOIN sys.indexes I ON T.[object_id] = I.[object_id]
	JOIN sys.dm_db_partition_stats PS ON I.[object_id] = PS.[object_id] AND I.index_id = PS.index_id;

-- This one will show data compression options as well (note that it will look weird if a partitioned table has a mix of compressed and noncompressed partitions).
SELECT
	S.[name] AS SchemaName
	,T.[name] AS TableName
	,I.[name] AS IndexName
	,I.index_id
	,I.[type_desc]
	,P.[data_compression_desc]
	,SUM(PS.used_page_count) * 8 / 1024.0 AS IndexSizeMB
	,COUNT(*) AS [Partitions]
	,MAX(PS.row_count) AS [Rows]
FROM
	sys.schemas S
	JOIN sys.tables T ON S.[schema_id] = T.[schema_id]
	JOIN sys.indexes I ON T.[object_id] = I.[object_id]
	JOIN sys.dm_db_partition_stats PS ON I.[object_id] = PS.[object_id] AND I.index_id = PS.index_id
	JOIN sys.partitions P ON PS.[partition_id] = P.[partition_id]
GROUP BY
	S.[name]
	,T.[name]
	,I.[name]
	,I.index_id
	,I.[type_desc]
	,P.[data_compression_desc]
ORDER BY
	S.[name]
	,T.[name]
	
-- This one will show data compression options as well (note that it will look weird if a partitioned table has a mix of compressed and noncompressed partitions).
SELECT
	S.[name] AS SchemaName
	,T.[name] AS TableName
	,I.[name] AS IndexName
	,I.index_id
	,I.[type_desc]
	,P.[data_compression_desc]
	,SUM(PS.in_row_data_page_count) * 8 / 1024.0 AS InRowDataMB
	,SUM(PS.in_row_used_page_count) * 8 / 1024.0 AS InRowUsedMB
	,SUM(PS.in_row_reserved_page_count) * 8 / 1024.0 AS InRowReservedMB
	,SUM(PS.lob_used_page_count) * 8 / 1024.0 AS LOBUsedMB
	,SUM(PS.lob_reserved_page_count) * 8 / 1024.0 AS LOBReservedMB
	,SUM(PS.row_overflow_used_page_count) * 8 / 1024.0 AS RowOverflowUsedMB
	,SUM(PS.row_overflow_reserved_page_count) * 8 / 1024.0 AS RowOverflowReservedMB
	,SUM(PS.used_page_count) * 8 / 1024.0 AS UsedSizeMB
	,SUM(PS.reserved_page_count) * 8 / 1024.0 AS ReservedSizeMB
	,COUNT(*) AS [Partitions]
	,SUM(PS.row_count) AS [Rows]
FROM
	sys.schemas S
	JOIN sys.tables T ON S.[schema_id] = T.[schema_id]
	JOIN sys.indexes I ON T.[object_id] = I.[object_id]
	JOIN sys.dm_db_partition_stats PS ON I.[object_id] = PS.[object_id] AND I.index_id = PS.index_id
	JOIN sys.partitions P ON PS.[partition_id] = P.[partition_id] AND PS.[object_id] = P.[object_id] AND PS.[index_id] = P.[index_id]
GROUP BY
	S.[name]
	,T.[name]
	,I.[name]
	,I.index_id
	,I.[type_desc]
	,P.[data_compression_desc]
ORDER BY
	SUM(PS.used_page_count) DESC;